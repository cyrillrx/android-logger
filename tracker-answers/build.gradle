buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.22.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'bintray-release'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'http://dl.bintray.com/cyrillrx/maven' }
}

def moduleVersion = '0.4.0'

android {
    compileSdkVersion project.ANDROID_BUILD_SDK_VERSION as int
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION as int
        targetSdkVersion project.ANDROID_BUILD_TARGET_SDK_VERSION as int
        versionCode 1
        versionName moduleVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        lintConfig file('../lint.xml')
    }

    testOptions {
        unitTests {
            returnDefaultValues = true

            all {
                // Configure whether failing tests should fail the build
                ignoreFailures false

                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"
                    outputs.upToDateWhen { false }
                    showStandardStreams = true
                }
            }
        }
    }
}

dependencies {
    compile "com.cyrillrx:tracker:${trackerVersion}"
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') { transitive = true; }

    testCompile 'junit:junit:4.12'

    androidTestCompile 'com.android.support:support-annotations:25.0.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}


publish {
    userOrg = 'cyrillrx'
    groupId = 'com.cyrillrx.android'
    artifactId = 'tracker-answers'
    version = moduleVersion
    description = 'Answer extention for lrx tracker.'
    website = 'https://github.com/cyrillrx/android_logger'
}
