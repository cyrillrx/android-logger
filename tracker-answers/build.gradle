buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'io.fabric.tools:gradle:1.31.0'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'bintray-release'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'http://dl.bintray.com/cyrillrx/maven' }
}

android {
    compileSdkVersion project.compileSdkVersion

    defaultConfig {
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        lintConfig file('../lint.xml')
    }

    testOptions {
        unitTests {
            returnDefaultValues = true

            all {
                // Configure whether failing tests should fail the build
                ignoreFailures false

                testLogging {
                    events "passed", "skipped", "failed", "standardOut", "standardError"
                    outputs.upToDateWhen { false }
                    showStandardStreams = true
                }
            }
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "com.cyrillrx:tracker:$trackerVersion"
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') { transitive = true }

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.25.0'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
}

publish {
    // Bintray credentials
    bintrayUser = project.bintrayKey
    bintrayKey = project.bintrayKey

    // Repository config
    userOrg = project.moduleOrg
    groupId = project.moduleGroupId
    website = project.moduleWebsite
    artifactId = 'tracker-answers'
    publishVersion = project.trackerVersion
    desc = 'Answer extention for lrx tracker.'
}
